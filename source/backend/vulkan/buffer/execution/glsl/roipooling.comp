#version 440 core
layout(std430) buffer;
layout(set = 0, binding = 0) writeonly buffer outputBuffer{
    vec4 data[];
}uOutput;

layout(set = 0, binding = 1) readonly buffer inputBuffer{
    vec4 data[];
}uInput;

layout(set = 0, binding = 2) readonly buffer roiBuffer{
    float data[];
}uRoI;

layout(set = 0, binding = 3) uniform constBuffer
{
	ivec4 inputImgSize;
	ivec4 outputImgSize;
	float spatialScale;
}uRoiPoolingParam;

layout(local_size_x = 8, local_size_y = 8) in;

void main()
{
	ivec3 pos = ivec3(gl_GlobalInvocationID);
	ivec3 outputImgSize = uRoiPoolingParam.outputImgSize.xyz;

	if(pos.x < outputImgSize.x && pos.y < outputImgSize.y)
	{
		ivec3 inputImgSize = uRoiPoolingParam.inputImgSize.xyz;
		int roiBatchIndex = pos.z % uRoiPoolingParam.outputImgSize.w;
		int inputZIndex = pos.z / uRoiPoolingParam.outputImgSize.w;
		// 0, xmin, ymin, xmax, ymax
		float roiData0 = uRoI.data[roiBatchIndex * 5 + 0];
		float roiData1 = uRoI.data[roiBatchIndex * 5 + 1];
		float roiData2 = uRoI.data[roiBatchIndex * 5 + 2];
		float roiData3 = uRoI.data[roiBatchIndex * 5 + 3];
		float roiData4 = uRoI.data[roiBatchIndex * 5 + 4];
		int inputBatch = int(roiData0);
		int x1 = int(round(float(roiData1) * uRoiPoolingParam.spatialScale));
		int y1 = int(round(float(roiData2) * uRoiPoolingParam.spatialScale));
		int x2 = int(round(float(roiData3) * uRoiPoolingParam.spatialScale));
		int y2 = int(round(float(roiData4) * uRoiPoolingParam.spatialScale));

		int roiW = max(x2 - x1 + 1, 1);
		int roiH = max(y2 - y1 + 1, 1);
		float binSizeW = float(roiW) / float(outputImgSize.x);
		float binSizeH = float(roiH) / float(outputImgSize.y);

		int wStart = clamp(x1 + int(floor(float(pos.x) * binSizeW)), 0, inputImgSize.x);
		int wEnd = clamp(x1 + int(ceil(float(pos.x + 1) * binSizeW)), 0, inputImgSize.x);
		int hStart = clamp(y1 + int(floor(float(pos.y) * binSizeH)), 0, inputImgSize.y);
		int hEnd = clamp(y1 + int(ceil(float(pos.y + 1) * binSizeH)), 0, inputImgSize.y);

		bool isEmpty = (hEnd <= hStart) || (wEnd <= wStart);

		int inputBasicOffset = inputBatch * uRoiPoolingParam.inputImgSize.x * uRoiPoolingParam.inputImgSize.y
			+ inputZIndex * uRoiPoolingParam.inputImgSize.x * uRoiPoolingParam.inputImgSize.y * uRoiPoolingParam.inputImgSize.w
			;
		int inputStartOffset = wStart
					+ hStart * uRoiPoolingParam.inputImgSize.x
					+ inputBasicOffset;
		vec4 res = isEmpty ? vec4(0.0) : uInput.data[inputStartOffset];
		for(int i = hStart; i < hEnd; ++i)
		{
			for(int j = wStart; j < wEnd; ++j)
			{
				int inputOffset = j
					+ i * uRoiPoolingParam.inputImgSize.x
					+ inputBasicOffset;
				res = max(res, uInput.data[inputOffset]);
			}
		}
		uOutput.data[0
			+ pos.x
			+ pos.y * uRoiPoolingParam.outputImgSize.x
			+ pos.z * uRoiPoolingParam.outputImgSize.x * uRoiPoolingParam.outputImgSize.y
		] = res;
	}
}

#version 440 core
layout(std140) buffer;
layout(set=0, binding=0) writeonly restrict uniform image2D uOutput;
layout(set=0, binding=1) uniform sampler2D uInput;


layout(set=0, binding=2) uniform constBuffer {
    ivec4 inputSize;
    ivec4 outputSize;
    ivec2 pad;
    ivec2 kernelSize;
    ivec2 stride;
} uConstant;


layout (local_size_x = 8, local_size_y = 8, local_size_z = 1) in;

void main()
{
    ivec3 pos = ivec3(gl_GlobalInvocationID);
    ivec3 outputSize = uConstant.outputSize.xyz;
    ivec2 spos = pos.xy*uConstant.stride-uConstant.pad;
    int batch = pos.z / uConstant.outputSize.z;
    int depth = pos.z % uConstant.outputSize.z;

    if (all(lessThan(ivec4(pos.x, pos.y, depth, batch), uConstant.outputSize)))
    {
        ivec2 inputSizeXY = uConstant.inputSize.xy;
        vec4 color = vec4(0.0);
        ivec2 sfxy = max(ivec2(0), -spos);
        ivec2 efxy = min(uConstant.kernelSize, inputSizeXY-spos);
        ivec2 size = efxy-sfxy;
        vec4 frac = vec4(1.0/float(size.x)/float(size.y));

        for (int fy=sfxy.y; fy<efxy.y; ++fy)
        {
            for (int fx=sfxy.x; fx<efxy.x; ++fx)
            {
                ivec2 spos_ = spos + ivec2(fx, fy);
                color += texelFetch(uInput, ivec2(spos_.x + depth * uConstant.inputSize.x, spos_.y + batch * uConstant.inputSize.y), 0);
            }
        }
        imageStore(uOutput, ivec2(pos.x + depth * uConstant.outputSize.x, pos.y + batch * uConstant.outputSize.y), frac*color);
    }
}
